buildscript {
    ext {
        testJacksonVersions = ['2.5.0', '2.6.0', '2.7.0', '2.8.0', '2.9.0', '2.10.0', '2.11.0']

        // external dependency versions
        groovyVersion = '3.0.0'
        gradleVersion = '6.5.1'
        jacksonVersion = "[${testJacksonVersions.last()},)"
        licensePluginVersion = '0.14.0'
        releasePluginVersion = '2.8.1'
        spockVersion = '2.0-M3-groovy-3.0'

        // external dependencies
        groovy = "org.codehaus.groovy:groovy-all:${groovyVersion}"
        jacksonDatabind = "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
        spockCore = "org.spockframework:spock-core:${spockVersion}"
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:${licensePluginVersion}"
        classpath "net.researchgate:gradle-release:${releasePluginVersion}"
    }
}

wrapper {
    gradleVersion = '6.5.1'
    distributionUrl = distributionUrl.replace("bin", "all")
}

group = 'com.github.jonpeterson'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'com.github.hierynomus.license'
apply plugin: 'net.researchgate.release'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile jacksonDatabind

    testCompile groovy
    testCompile spockCore
}

license {
    include '**/*.java'
    include '**/*.groovy'
    mapping {
        java = 'SLASHSTAR_STYLE'
        groovy = 'SLASHSTAR_STYLE'
    }
}

// don't run tests with default test task
test.excludes = ['**/*']

// dynamically set up test executions for each version of Jackson
testJacksonVersions.each { version ->
    def safeVersion = version.replaceAll('\\.', '_')

    configurations {
        "testJackson${safeVersion}Compile" {
            extendsFrom testCompile
        }
        "testJackson${safeVersion}Runtime" {
            extendsFrom testRuntime
            resolutionStrategy {
                force jacksonDatabind.replace(jacksonVersion, version)
            }
        }
    }

    sourceSets {
        "testJackson$safeVersion" {
            compileClasspath += sourceSets.main.output + sourceSets.test.output
            runtimeClasspath += sourceSets.main.output + sourceSets.test.output
        }
    }

    task "testJackson$safeVersion"(type: Test) {
        testClassesDirs = sourceSets.test.output.classesDirs
        classpath = sourceSets."testJackson$safeVersion".runtimeClasspath
    }

    test.dependsOn "testJackson$safeVersion"
}


task javadocJar(type: Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    def ossrhUsername = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : null
    def ossrhPassword = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : null

    doFirst {
        if(!ossrhUsername || !ossrhPassword)
            throw new MissingPropertyException('ossrhUsername or ossrhPassword not set')
    }

    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Jackson Model Versioning Module'
                packaging 'jar'
                description 'Jackson 2.x module for handling versioning of models.'
                url 'https://github.com/jonpeterson/jackson-module-model-versioning'

                scm {
                    connection 'scm:git:git://github.com/jonpeterson/jackson-module-model-versioning.git'
                    developerConnection 'scm:git:git@github.com:jonpeterson/jackson-module-model-versioning.git'
                    url 'https://github.com/jonpeterson/jackson-module-model-versioning'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'jonpeterson'
                        name 'Jonathan Peterson'
                        email 'jonathan.p.peterson@gmail.com'
                        url 'https://github.com/jonpeterson'
                    }
                }
            }
        }
    }
}
